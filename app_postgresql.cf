#Uncomment for testing: /var/cfengine/bin/cf-agent -K -I -f <full_path>
#body common control {
# bundlesequence => { "app_postgresql" };
#
# inputs => { 
#            "cfengine_stdlib.cf", 
#            "globals.cf",
#            "sys_versions.cf"
#           };
#}

bundle agent app_postgresql
{

classes:
 "sysgrp_cfengine" expression => classmatch("sysgrp_cfengine_.*");
 "postgresql" expression => classmatch("sysver_postgresql_.*");

methods:
  sysgrp_cfengine.postgresql::
    "any" usebundle => app_postgresql_packages;

  sysgrp_cfengine.sysver_postgresql_localdev::
    "any" usebundle => app_postgresql_conf("localdev");

  sysgrp_cfengine.sysver_postgresql_integration::
    "any" usebundle => app_postgresql_conf("integration");

  sysgrp_cfengine.sysver_postgresql_staging::
    "any" usebundle => app_postgresql_conf("staging");

  sysgrp_cfengine.sysver_postgresql_prod::
    "any" usebundle => app_postgresql_conf("prod");

processes:
  sysgrp_cfengine.postgresql::
    "postmaster"
      comment => "Ensure postgresql is running",
      action => if_elapsed(1),
      restart_class => "postgresql_restart";

commands:
  sysgrp_cfengine.postgresql.postgresql_restart::
    #The cmd_repair classes are necessary to ensure that the command will be executed.
    #If unique classes are not given and the same command has already been executed in
    #a previous promise, cfengine will optimize away the execution. This makes no sense,
    #but this is a work around until we get an explanation from CF.
    "/sbin/service postgresql-9.1 restart"
      action => if_elapsed(1),
      classes => cmd_repair("app_postgresql_restart", "app_postgresql_restart");
}

bundle agent app_postgresql_packages
{
vars:
 #wget http://yum.postgresql.org/9.1/redhat/rhel-5-x86_64/pgdg-centos91-9.1-4.noarch.rpm
 "postgresql_yum_repo" string => "pgdg-centos91-9.1-4.noarch.rpm";
 "postgresql_yum_repo_path" string => "$(globals.rpm_repo)/$(postgresql_yum_repo)";

 "postgresql_packages" slist => {
	"postgresql91",
	"postgresql91-libs",
	"postgresql91-server",
	"postgresql91-jdbc",
	"postgresql91-python",
	"postgresql91-devel",
	"postgresql91-contrib",
	"pgpool-II-91",
	"postgis91",
	"pg_top91",
	"slony1-91"
 };

files:
    "$(postgresql_yum_repo_path)"
      comment => "Sync postgrsql yum repo",
      perms => mog("644", "root", "root"),
      action => if_elapsed("$(globals.default_ifelapsed)"),
      copy_from => remote_dcp("$(postgresql_yum_repo_path)", "$(sys.policy_hub)");

    "/var/lib/pgsql/.bash_profile"
      comment => "Source 3ps profile in postgres bash_profile",
      create => "false",
      edit_line => append_if_no_lines("$(globals.default_3ps_profile_lines)");

packages:
    "pgdg-centos91"
      comment => "Install postgresql yum repo",
      action => if_elapsed("$(globals.default_ifelapsed)"),
      classes => if_repaired("postgresql_installed"),
      package_policy => "add",
      package_version => "9.1-4",
      package_architectures => { "noarch" },
      package_method => rpm_ifelapsed("$(globals.rpm_repo)");

    "$(postgresql_packages)"
      comment => "Install postgres packages",
      action => if_elapsed("$(globals.default_ifelapsed)"),
      classes => if_repaired("postgresql_installed"),
      package_policy => "add",
      package_method => yum_ifelapsed;

commands:
  postgresql_installed::

    "/sbin/service postgresql-9.1 initdb";	
    
    #The cmd_repair classes are necessary to ensure that the command will be executed.
    #If unique classes are not given and the same command has already been executed in
    #a previous promise, cfengine will optimize away the execution. This makes no sense,
    #but this is a work around until we get an explanation from CF.
    "/sbin/service postgresql-9.1 restart"
      action => if_elapsed(1),
      classes => cmd_repair("app_postgresql_packages_restart", "app_postgresql_packages_restart");

    #Wait for postgres to startup
    "/bin/sleep 30"
      action => if_elapsed(1),
      classes => cmd_repair("app_postgresql_packages_sleep", "app_postgresql_packages_sleep");

    "/usr/pgsql-9.1/bin/psql -c \"CREATE ROLE postgres\""
      action => if_elapsed(1),
      contain => postgresql_contain;

    "/usr/pgsql-9.1/bin/psql -c \"ALTER ROLE postgres with SUPERUSER INHERIT CREATEROLE CREATEDB LOGIN PASSWORD 'md5d567c3bd5b5af9c0b4f16da9320a0dac'\""
      action => if_elapsed(1),
      contain => postgresql_contain;

    "/sbin/chkconfig postgresql-9.1 on";
}


bundle agent app_postgresql_conf(env)
{

classes:
 "sysgrp_cfengine" expression => classmatch("sysgrp_cfengine_.*");

 #Only sync configuration files if the data directory already exists and is populated
 #from successful initdb
 "postgresql_data_exists" expression => fileexists("/var/lib/pgsql/9.1/data/postgresql.conf");

files:
  postgresql_data_exists::

    "/var/lib/pgsql/9.1/data/postgresql.conf"
      comment => "Install postgresql.conf",
      classes => if_repaired("postgresql_conf_repaired"),
      perms => mog("600", "postgres", "postgres"),
      action => if_elapsed("$(globals.default_ifelapsed)"),
      copy_from => remote_dcp("$(globals.masterfiles)/env/$(env)/postgresql/data/postgresql.conf", "$(sys.policy_hub)");

    "/var/lib/pgsql/9.1/data/pg_hba.conf"
      comment => "Install postgresql.conf",
      classes => if_repaired("postgresql_conf_repaired"),
      perms => mog("600", "postgres", "postgres"),
      action => if_elapsed("$(globals.default_ifelapsed)"),
      copy_from => remote_dcp("$(globals.masterfiles)/env/$(env)/postgresql/data/pg_hba.conf", "$(sys.policy_hub)");

commands:
  #only auto-restart postgres for localdev environment
  #it's too risky in other environments.
  sysgrp_cfengine.sysver_postgresql_localdev.postgresql_conf_repaired::
    #The cmd_repair classes are necessary to ensure that the command will be executed.
    #If unique classes are not given and the same command has already been executed in
    #a previous promise, cfengine will optimize away the execution. This makes no sense,
    #but this is a work around until we get an explanation from CF.
    "/sbin/service postgresql-9.1 restart"
      action => if_elapsed(1),
      classes => cmd_repair("app_postgresql_conf_restart", "app_postgresql_conf_restart");
}

#execute commands as postgres user
body contain postgresql_contain
{
useshell => "no";
exec_owner => "postgres";
}
