#Uncomment for testing: /var/cfengine/bin/cf-agent -K -I -f <full_path>
#body common control {
# bundlesequence => { "app_techresidents_web" };
#
# inputs => { 
#            "cfengine_stdlib.cf", 
#            "globals.cf",
#            "sys_versions.cf",
#            "sys_repos.cf"
#           };
#}

bundle agent app_techresidents_web
{

vars:
 "web_root" string => "$(globals.our_root)/www";
 "web_site" string => "techresidents.com";
 "web_app" string => "techresidents_web";
 "web_app_arch" string => "x86_64";

classes:
 "sysgrp_cfengine" expression => classmatch("sysgrp_cfengine_.*");
 "techresidents_web" expression => classmatch("sysver_techresidents_web_.*");

methods:
  sysgrp_cfengine.sysver_techresidents_web_localdev::
    "any" usebundle => sys_repo_sync("$(web_app)", "$(python.techresidents_web_localdev_version)", "$(python.techresidents_web_localdev_release)", "$(web_app_arch)");
    "any" usebundle => sys_packages_rpm_multiversion("$(web_app)", "$(python.techresidents_web_localdev_version)", "$(python.techresidents_web_localdev_release)", "$(web_app_arch)");
    "any" usebundle => app_techresidents_web_conf("localdev", "$(web_root)", "$(web_site)", "$(web_app)", "$(python.techresidents_web_localdev_version)");

  sysgrp_cfengine.sysver_techresidents_web_integration::
    "any" usebundle => sys_repo_sync("$(web_app)", "$(python.techresidents_web_integration_version)", "$(python.techresidents_web_integration_release)", "$(web_app_arch)");
    "any" usebundle => sys_packages_rpm_multiversion("$(web_app)", "$(python.techresidents_web_integration_version)", "$(python.techresidents_web_integration_release)", "$(web_app_arch)");
    "any" usebundle => app_techresidents_web_conf("integration", "$(web_root)", "$(web_site)", "$(web_app)","$(python.techresidents_web_integration_version)");

  sysgrp_cfengine.sysver_techresidents_web_staging::
    "any" usebundle => sys_repo_sync("$(web_app)", "$(python.techresidents_web_staging_version)", "$(python.techresidents_web_staging_release)", "$(web_app_arch)");
    "any" usebundle => sys_packages_rpm_multiversion("$(web_app)", "$(python.techresidents_web_staging_version)", "$(python.techresidents_web_staging_release)", "$(web_app_arch)");
    "any" usebundle => app_techresidents_web_conf("staging", "$(web_root)", "$(web_site)", "$(web_app)", "$(python.techresidents_web_staging_version)");

  sysgrp_cfengine.sysver_techresidents_web_prod::
    "any" usebundle => sys_repo_sync("$(web_app)", "$(python.techresidents_web_prod_version)", "$(python.techresidents_web_prod_release)", "$(web_app_arch)");
    "any" usebundle => sys_packages_rpm_multiversion("$(web_app)", "$(python.techresidents_web_prod_version)", "$(python.techresidents_web_prod_release)", "$(web_app_arch)");
    "any" usebundle => app_techresidents_web_conf("prod", "$(web_root)", "$(web_site)", "$(web_app)", "$(python.techresidents_web_prod_version)");

processes:
  sysgrp_cfengine.techresidents_web::
    "httpd"
      comment => "Ensure httpd is running",
      action => if_elapsed(1),
      restart_class => "restart_httpd";

commands:
  sysgrp_cfengine.restart_httpd::
    "/sbin/service httpd restart";	
}

bundle agent app_techresidents_web_conf(env, web_root, web_site, web_app, web_app_version)
{
classes:
  "sysgrp_cfengine" expression => classmatch("sysgrp_cfengine_.*");
  "is_localdev" expression => regcmp("localdev", "$(env)");

files:
    "$(web_root)/."
      comment => "Create web root directory",
      perms => mog("644", "root", "root"),
      create => "true";

    "$(web_root)/$(web_site)/$(env)/log/."
      comment => "Create web app log directory",
      perms => mog("644", "$(globals.default_user)", "$(globals.default_group)"),
      create => "true";

    "$(web_root)/$(web_site)/$(env)/$(web_app)"
      comment => "Create web app symlink",
      move_obstructions => "true",
      link_from => ln_web("$(web_root)/$(web_site)/install/$(web_app)-$(web_app_version)/$(web_app)");

    "/etc/httpd/conf.d/$(web_site).$(env).conf"
      comment => "Install apache configuration httpd.conf",
      classes => if_repaired("apache_conf_repaired"),
      action => if_elapsed("$(globals.default_ifelapsed)"),
      copy_from => remote_dcp("$(globals.masterfiles)/env/$(env)/apache/conf.d/$(web_site).conf", "$(sys.policy_hub)");
 
 
  #if localdev host and environment is set to localdev additionally configure the following
  localdev.is_localdev::
    "$(web_root)/localdev.com/ssl"
      comment => "Install localdev ssl certs",
      perms => mog("600", "root", "root"),
      action => if_elapsed("$(globals.default_ifelapsed)"),
      copy_from => remote_dcp("$(globals.masterfiles)/env/localdev/apache/ssl", "$(sys.policy_hub)"),
      depth_search => recurse("inf");

    "$(web_root)/localdev.com/localdev/."
      comment => "Create localdev.com localdev env directory",
      perms => mog("644", "root", "root"),
      create => "true";

    "$(web_root)/localdev.com/localdev/test"
      move_obstructions => "true",
      link_from => ln_web("$(web_root)/$(web_site)/install/$(web_app)-$(web_app_version)/$(web_app)");

commands:
  #only auto-restart apache for localdev environment
  #it's too risky in other environments.
  sysgrp_cfengine.localdev.is_localdev.apache_conf_repaired::
    
    #The cmd_repair classes are necessary to ensure that the command will be executed.
    #If unique classes are not given and the same command has already been executed in
    #a previous promise, cfengine will optimize away the execution. This makes no sense,
    #but this is a work around until we get an explanation from CF.
    "/sbin/service httpd restart"
      classes => cmd_repair("app_techresidents_web_conf_restart", "app_techresidents_web_conf_restart");
}

body link_from ln_web(source)
{
  link_type => "symlink";
  source => "$(source)";
}
